       IDENTIFICATION DIVISION.

      * DESCRIPTION DU PROGRAMME :
      * RECUPERE LES LABELS DES MESSAGES D'ERREUR A PARTIR D'UN CODE,
      * DEPUIS UNE TABLE STOCKEE DANS UN FICHIER KSDS

       PROGRAM-ID. MAJASLB2.
       AUTHOR. QUENTIN POTEREK.
       DATE-WRITTEN. 17 DECEMBRE 2021.

      * --------------------
      * ENVIRONMENT DIVISION
      * --------------------

       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       SOURCE-COMPUTER.
           IBM-370.
      *    IBM-370 WITH DEBUGGING MODE.
       OBJECT-COMPUTER. IBM-370.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT MESS ASSIGN TO KSDSMESS
             ORGANIZATION IS INDEXED
             ACCESS MODE IS RANDOM
             RECORD KEY IS MESS-CODE
             FILE STATUS WS-FS-MESS.

      * -------------
      * DATA DIVISION
      * -------------

       DATA DIVISION.

       FILE SECTION.
       FD MESS.
       01 EMESS.
         02 MESS-CODE  PIC 999.
         02 MESS-LABEL PIC X(60).
         02 FILLER     PIC X(17).

       WORKING-STORAGE SECTION.
       77 WS-FS-MESS PIC XX.

       LINKAGE SECTION.
       01 LS-PROC  PIC 9.
       01 LS-CODE  PIC 999.
       01 LS-LABEL PIC X(60).

      * ------------------
      * PROCEDURE DIVISION
      * ------------------

       PROCEDURE DIVISION USING LS-PROC LS-CODE LS-LABEL.
           PERFORM GERER-MESS
           IF LS-PROC = 2
             MOVE LS-CODE TO MESS-CODE
             READ MESS
               INVALID KEY DISPLAY 'CLE INVALIDE: ' LS-CODE '!'
               NOT INVALID KEY MOVE MESS-LABEL TO LS-LABEL
             END-READ
           END-IF
           GOBACK
           .

       GERER-MESS.
           EVALUATE LS-PROC
             WHEN 0
               OPEN INPUT MESS
               PERFORM FST-MESS
             WHEN 1
               CLOSE MESS
               PERFORM FST-MESS
             WHEN 2
               CONTINUE
             WHEN OTHER
               DISPLAY 'ERREUR SP2 - LS-PROC INVALIDE: ' LS-PROC '!'
           END-EVALUATE
           .

       FST-MESS.
           IF WS-FS-MESS NOT = '00'
             DISPLAY 'ERREUR FICHIER MESS: ' WS-FS-MESS
             MOVE 16 TO RETURN-CODE
             GOBACK
           END-IF
           .

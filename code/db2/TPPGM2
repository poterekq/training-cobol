       IDENTIFICATION DIVISION.
       PROGRAM-ID. TPPGM2.
       AUTHOR. QUENTIN POTEREK.
       DATE-WRITTEN. 05 JANVIER 2022.


       ENVIRONMENT DIVISION.


       DATA DIVISION.

       WORKING-STORAGE SECTION.

      *STRUCTURE DE LA TABLE SQLCA
           EXEC SQL
             INCLUDE SQLCA
           END-EXEC.

      *STRUCTURE DE LA TABLE EMPLOYE
           EXEC SQL
             INCLUDE EMPLOYE
           END-EXEC.

      *VARIABLES INDICATEURS POUR EMPLOYE
       01 IND-COM PIC S9(4) COMP.

      *VARIABLES UTILISEES PAR DSNTIAR
       01 ERR-MSG.
         02 ERR-LONG PIC S9(4) COMP VALUE +720.
         02 ERR-TXT  PIC X(75) OCCURS 10 TIMES.

       01 ERR-TXT-LONG PIC 99 VALUE 72.

      *VARIABLES UTILITAIRES
       01 I PIC 99.

       01 TAUX PIC S9(4)V99 COMP-3 VALUE 1.05.


       PROCEDURE DIVISION.
           PERFORM RACINE
           GOBACK
           .

      * -------------------------------------------------------------- *

      * PARAGRAPHES UTILITAIRES

       PARAG-ERR.
           DISPLAY 'ERREUR DB2'
           DISPLAY 'APPEL DSNTIAR (MISE EN FORME SQLCA)'
           CALL 'DSNTIAR' USING SQLCA, ERR-MSG, ERR-TXT-LONG
           PERFORM VARYING I FROM 1 BY 1 UNTIL I > 10
              DISPLAY ERR-TXT (I)
           END-PERFORM
           STOP RUN.

      *CORPS D'EXECUTION DU PROGRAMME

       RACINE.
           EXEC SQL
             WHENEVER SQLERROR GOTO :PARAG-ERR
           END-EXEC
           PERFORM 000-INSERER-MARTIN
           PERFORM 100-CHANGER-SAL-COM
           PERFORM 200-SUPPRIMER-INFO
           PERFORM 300-AFFICHER-TABLE
           .

       000-INSERER-MARTIN.
           EXEC SQL
             INSERT INTO EMPLOYE
               VALUES ('90', 'MARTIN', 'E10', '1990-02-10', 3000, NULL)
           END-EXEC
           .

       100-CHANGER-SAL-COM.
           EXEC SQL
             UPDATE EMPLOYE
             SET SAL = SAL * :TAUX,
                 COM = NULL
             WHERE NOD = 'P02'
           END-EXEC
           .

       200-SUPPRIMER-INFO.
           EXEC SQL
             DELETE FROM EMPLOYE
             WHERE NOD =
               (SELECT NOD
                FROM DEPARTEMENT
                WHERE NDE = 'INFO')
           END-EXEC
           .

       300-AFFICHER-TABLE.
           EXEC SQL
             DECLARE C_EMPLOYE CURSOR FOR
               SELECT MAT, NOM, NOD, DAT, SAL, COM
               FROM EMPLOYE
             FOR FETCH ONLY
           END-EXEC

           EXEC SQL
             OPEN C_EMPLOYE
           END-EXEC

           PERFORM 310-RECUPERER-LIGNE

           PERFORM UNTIL SQLCODE NOT = ZERO
             PERFORM 320-AFFICHER-LIGNE
             PERFORM 310-RECUPERER-LIGNE
           END-PERFORM

           EXEC SQL
             CLOSE C_EMPLOYE
           END-EXEC
           .

       310-RECUPERER-LIGNE.
           EXEC SQL
             FETCH C_EMPLOYE
             INTO :MAT, :NOM, :NOD, :DAT, :SAL, :COM:IND-COM
           END-EXEC
           .

       320-AFFICHER-LIGNE.
           IF IND-COM NOT = 0
             MOVE 0 TO COM
           END-IF
           DISPLAY MAT ' ' NOM ' ' NOD ' ' DAT ' ' SAL ' ' COM
           .

       IDENTIFICATION DIVISION.
       PROGRAM-ID. TPPGM12.
       AUTHOR. QUENTIN POTEREK.
       DATE-WRITTEN. 05 JANVIER 2022.


       ENVIRONMENT DIVISION.


       DATA DIVISION.

       WORKING-STORAGE SECTION.

      *STRUCTURE DE LA TABLE SQLCA
           EXEC SQL
             INCLUDE SQLCA
           END-EXEC.

      *STRUCTURE DE LA TABLE EMPLOYE
           EXEC SQL
             INCLUDE EMPLOYE
           END-EXEC.

      *VARIABLES INDICATEURS POUR EMPLOYE
       01 IND-COM PIC S9(4) COMP.

      *VARIABLES UTILISEES PAR DSNTIAR
       01 ERR-MSG.
         02 ERR-LONG PIC S9(4) COMP VALUE +720.
         02 ERR-TXT  PIC X(75) OCCURS 10 TIMES.

       01 ERR-TXT-LONG PIC 99 VALUE 72.

      *VARIABLES UTILITAIRES
       01 I PIC 99.


       PROCEDURE DIVISION.
           PERFORM RACINE
           GOBACK
           .

      * -------------------------------------------------------------- *

      * PARAGRAPHES UTILITAIRES

       PARAG-ERR.
           DISPLAY 'ERREUR DB2'
           DISPLAY 'APPEL DSNTIAR (MISE EN FORME SQLCA)'
           CALL 'DSNTIAR' USING SQLCA, ERR-MSG, ERR-TXT-LONG
           PERFORM VARYING I FROM 1 BY 1 UNTIL I > 10
              DISPLAY ERR-TXT (I)
           END-PERFORM
           STOP RUN.

      *CORPS D'EXECUTION DU PROGRAMME

       RACINE.
           EXEC SQL
             WHENEVER SQLERROR GOTO :PARAG-ERR
           END-EXEC
           PERFORM Q2
           .

       Q2.
           PERFORM Q2-DECLARER-CURSEUR
           PERFORM OUVRIR-CURSEUR
           PERFORM Q2-ITERER-BD
           PERFORM FERMER-CURSEUR
           .

       Q2-DECLARER-CURSEUR.
           EXEC SQL
             DECLARE C_EMPLOYE CURSOR FOR
               SELECT NOM, SAL, COM
               FROM EMPLOYE
               ORDER BY NOM DESC
             FOR FETCH ONLY
           END-EXEC
           .

       Q2-ITERER-BD.
           PERFORM Q2-RECUPERER-LIGNE
           PERFORM UNTIL SQLCODE NOT = ZERO
             PERFORM Q2-AFFICHER-LIGNE
             PERFORM Q2-RECUPERER-LIGNE
           END-PERFORM
           .

       Q2-RECUPERER-LIGNE.
           EXEC SQL
             FETCH C_EMPLOYE
             INTO :NOM, :SAL, :COM:IND-COM
           END-EXEC
           .

       Q2-AFFICHER-LIGNE.
           IF IND-COM = 0
             DISPLAY NOM ' ' SAL ' ' COM
           ELSE
             DISPLAY 'ERREUR POUR ' NOM
           END-IF
           .

      *GESTION DES CURSEURS

       OUVRIR-CURSEUR.
           EXEC SQL
             OPEN C_EMPLOYE
           END-EXEC
           .

       FERMER-CURSEUR.
           EXEC SQL
             CLOSE C_EMPLOYE
           END-EXEC
           .

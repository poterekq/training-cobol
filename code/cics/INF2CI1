       IDENTIFICATION DIVISION.
       PROGRAM-ID. INF2CI1.

      *======================================================*
      *   E N V I R O N M E N T       D I V I S I O N        *
      *======================================================*
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER. IBM-390.
       OBJECT-COMPUTER. IBM-390.

      *======================================================*
      *           D A T A         D I V I S I O N            *
      *======================================================*
       DATA DIVISION.

       WORKING-STORAGE SECTION.

      *------------------------------------------------------*
      *   DESCRIPTION DETAILLEE DE LA ZONE DE COMMUNICATION  *
      *------------------------------------------------------*
       01  WS-COMMAREA.
           05 PROG-PRECEDENT      PIC X(8).
           05 PROG-COURANT        PIC X(8).
           05 PROG-SUIVANT        PIC X(8).
           05 FILLER              PIC X(76).

       01 C-R                     PIC S9(8) COMP.
       01  MESSAGE-TXT            PIC X(79).

       COPY MAP2CI2.
      *------------------------------------------------------*
      *   DESCRIPTION   DES  TOUCHES   FONCTIONS             *
      *------------------------------------------------------*
       COPY DFHAID.

       COPY  FSTAG.
       LINKAGE SECTION.
       01  DFHCOMMAREA.
           05 LK-COMMAREA            PIC X(100).

      *======================================================*
      *     P R O C E D U R E     D I V I S I O N            *
      *======================================================*

       PROCEDURE DIVISION.
       00000-INIT-PROGRAMME.
      *---------------------*
            PERFORM  10000-DEBUT-PROGRAMME
            PERFORM  20000-TRAIT-PROGRAMME
            GOBACK.


       10000-DEBUT-PROGRAMME.
      *---------------------*
      *   TEST DE PREMIERE ENTREE DANS LE PROGRAMME
      *   SI NON PROGRAMME  MENU ===>  ARRET  (ACCES INTERDIT)
           IF EIBCALEN = 0
              INITIALIZE      WS-COMMAREA
           ELSE
              MOVE LK-COMMAREA  TO WS-COMMAREA
           END-IF

           MOVE  PROG-COURANT     TO  PROG-PRECEDENT
           MOVE  'INF2CI1'        TO  PROG-COURANT
           .

       20000-TRAIT-PROGRAMME.
      *---------------------*

           IF PROG-PRECEDENT  NOT =  PROG-COURANT
              PERFORM  21000-TRAIT-CHARGEMENT
              PERFORM  22000-TRAIT-ENVOI
           ELSE
              PERFORM  23000-TRAIT-RECEPTION
           END-IF
           .
      *********************************************************

       21000-TRAIT-CHARGEMENT.
      *----------------------*
      *  CHARGEMENT DE LA MAP AVANT AFFICHAGE
           MOVE LOW-VALUE TO MAP2O

      *  SI TRAITEMENT PARTICULIER AVANT AFFICHAGE
      *    PERFORM  21100-TRAIT-SPECIFIQUE
           .
      *21100-TRAIT-SPECIFIQUE.
      *----------------------*
      *    ..............................
      *    ..............................
      *    ..............................
      *    ..............................
      *    .


       22000-TRAIT-ENVOI.
      *-----------------*
           EXEC CICS SEND MAP    ('MAP2')
                          MAPSET ('MAP2CI2')
                          ERASE
           END-EXEC

           MOVE PROG-COURANT TO PROG-SUIVANT

           EXEC CICS RETURN TRANSID  ('T2C1')
                            COMMAREA (WS-COMMAREA)
                            LENGTH   (LENGTH OF WS-COMMAREA)
           END-EXEC
           .

       23000-TRAIT-RECEPTION.
      *---------------------*

           EVALUATE EIBAID
              WHEN DFHENTER
                   PERFORM  23100-TRAIT-ENTER
              WHEN DFHCLEAR
              WHEN DFHPF12
                   PERFORM  23200-TRAIT-FIN
              WHEN DFHPF3
                   PERFORM  23120-PROG-PRECEDENT
              WHEN OTHER
                   PERFORM  90000-ERR-TOUCHE
           END-EVALUATE
           .
       23100-TRAIT-ENTER.
      *------------------*
      *    ..........................
           EXEC CICS RECEIVE MAP    ('MAP2')
                             MAPSET ('MAP2CI2')
                             RESP (C-R)
           END-EXEC

           EVALUATE C-R
              WHEN DFHRESP(NORMAL)
                   CONTINUE
              WHEN DFHRESP(MAPFAIL)
                   MOVE 'SAISIR NUM SVP  ' TO MSGO
                   PERFORM  22000-TRAIT-ENVOI
              WHEN OTHER
                   PERFORM 91000-ERREUR-CICS
           END-EVALUATE
           DISPLAY '=====> NUMERO '  NUMI
           EXEC CICS  READ FILE('FSTAG02')
                           INTO(E-NUMERO)
                           RIDFLD(NUMI)
                           LENGTH(LENGTH OF E-STAGIAIRE)
                           RESP(C-R)
           END-EXEC

           EVALUATE C-R
              WHEN DFHRESP(NORMAL)
                   MOVE  E-NUMERO  TO   NUMO
                   MOVE  E-NOM     TO   NOMO
                   MOVE  E-PRENOM  TO   PRENOMO
                   MOVE  E-ADR1    TO   ADRESSO
                   MOVE  E-CODEP   TO   CPO
                   MOVE  E-VILLE   TO   VILLEO
                   MOVE  E-TELDOM  TO   TELFO
                   MOVE  E-TELPOR  TO   TELPO
                   MOVE  E-DATE-NAISS TO DADNO
      *========>   AFFICHAGE ENGT
      *========>   MOVE E-NUMERO   TO  NUMO
      *========>   MOVE E-XXXXXX   TO  XXXO
                   PERFORM  22000-TRAIT-ENVOI
              WHEN DFHRESP(NOTFND)
                   MOVE 'ENGT NON TROUVE ' TO MSGO
                   PERFORM  22000-TRAIT-ENVOI
              WHEN OTHER
                   PERFORM 91000-ERREUR-CICS
           END-EVALUATE
           .

       23110-PROG-SUIVANT.
      *------------------*
           EXEC CICS XCTL  PROGRAM  (PROG-SUIVANT)
                           COMMAREA (WS-COMMAREA)
                           LENGTH   (LENGTH OF WS-COMMAREA)
           END-EXEC
           .


       23120-PROG-PRECEDENT.
      *--------------------*
           MOVE 'INF0CI1' TO PROG-PRECEDENT
           EXEC CICS XCTL  PROGRAM  (PROG-PRECEDENT)
                           COMMAREA (WS-COMMAREA)
                           LENGTH   (LENGTH OF WS-COMMAREA)
           END-EXEC
           .

       23200-TRAIT-FIN.
      *---------------*
           MOVE 'FIN DE LA TRANSACTION' TO MESSAGE-TXT
           PERFORM   99000-FIN-CICS.


       90000-ERR-TOUCHE.
      *----------------*
           MOVE 'TOUCHE DE FONCTION INVALIDE' TO MSGO
           PERFORM  22000-TRAIT-ENVOI.


       91000-ERREUR-CICS.
      *-------------------*
           MOVE 'ERREUR CICS !!!  FIN DE LA TRANSACTION' TO MESSAGE-TXT
           PERFORM   99000-FIN-CICS.

       99000-FIN-CICS.
      *--------------*
           EXEC CICS SEND FROM   (MESSAGE-TXT)
                          LENGTH (LENGTH OF MESSAGE-TXT)
                          ERASE
           END-EXEC

           EXEC CICS RETURN END-EXEC
           .

       IDENTIFICATION DIVISION.

       PROGRAM-ID. EXOSTR.
       AUTHOR. QUENTIN POTEREK.
       DATE-WRITTEN. 09 DECEMBRE 2021.

      * --------------------
      * ENVIRONMENT DIVISION
      * --------------------

       ENVIRONMENT DIVISION.

      * -------------
      * DATA DIVISION
      * -------------

       DATA DIVISION.
       WORKING-STORAGE SECTION.
      * PARAMETRE SYSIN UTILISE POUR LE CONVERTING
       77 CHG-CHAR  PIC X.

      * PARAMETRE DE CONTROLE POUR LE STRING
       77 WS-STRING PIC X(80).
       77 WS-PTR    PIC 99 VALUE 7.
       77 WS-PTR-IN PIC X(5) VALUE 'HELLO'.

      * ZONES DE CONTROLE POUR LE UNSTRING
       01 ZONES.
         02 Z1      PIC X(20).
         02 Z2      PIC X(20).
         02 Z3      PIC X(20).
         02 Z4      PIC X(20).
       01 CPT.
         02 CPT1    PIC 99.
         02 CPT2    PIC 99.
         02 CPT3    PIC 99.
         02 CPT4    PIC 99.
       01 DLTR.
         02 DLTR1   PIC X.
         02 DLTR2   PIC X.
         02 DLTR3   PIC X.
         02 DLTRCPT PIC 99.

       LINKAGE SECTION.
       01 PARM.
         02 PARM-LENGTH  PIC S9(4) COMP-3.
         02 PARM-CONTENT PIC X(80).

      * ------------------
      * PROCEDURE DIVISION
      * ------------------

       PROCEDURE DIVISION USING PARM.
      * INITIALISATION DE L'ENSEMBLE DES ZONES UTILISEES
           PERFORM INITIALIZE-VAR

      * FOURNIR AU SYSIN UN CARACTERE DE LA CHAINE PARM A MODIFIER
           ACCEPT CHG-CHAR

      * IMPRESSION DE L'EN-TETE DU RAPPORT
           DISPLAY 'MANIPULATION DES CHAINES :'
           DISPLAY '--------------------------'
           DISPLAY ' '

      * IMPRESSION DES PARAMETRES UTILISES POUR LE TRAITEMENT
           DISPLAY 'PARAMETRES D''ENTREE: ' PARM-CONTENT
           DISPLAY 'CARACTERE SYSIN A MODIFIER: ' CHG-CHAR
           DISPLAY ' '

      * CONVERSION DU PARAMETRE SYSIN EN UN 1
           INSPECT PARM-CONTENT CONVERTING CHG-CHAR TO '1'
           DISPLAY 'CHAINE APRES MODIFICATION: ' PARM-CONTENT
           DISPLAY ' '

      * DEPACKETAGE DU PARAMETRE D'ENTREE ET AFFICAGE DES ZONES
           PERFORM RECUPERER-PARM
           DISPLAY 'UNSTRING DES PARAMETRES D''ENTREE'
           DISPLAY 'DELIMITEURS UTILISES: ' DLTR1 ' ' DLTR2 ' ' DLTR3
           DISPLAY 'ZONE 1: ' Z1 'CPT1: ' CPT1
           DISPLAY 'ZONE 2: ' Z2 'CPT2: ' CPT2
           DISPLAY 'ZONE 3: ' Z3 'CPT3: ' CPT3
           DISPLAY 'ZONE 4: ' Z4 'CPT4: ' CPT4
           DISPLAY ' '

      * CONCATENATION DES ZONES AVEC STRING
           STRING Z1 '---' Z2 '---' Z3 '---' Z4
             DELIMITED BY SPACE
             INTO WS-STRING
           END-STRING
           DISPLAY 'ASSEMBLAGE DES ZONES EN UNE CHAINE UNIQUE'
           DISPLAY 'STRING: ' WS-STRING
           DISPLAY ' '

      * UTILISATION DU TALLYING POUR COMPTER LES D
           MOVE 0 TO CPT4
           INSPECT Z4 TALLYING CPT4 FOR ALL 'D'
           DISPLAY 'TAILLE DE Z4 SELON TALLYING SUR D'
           DISPLAY 'CPT4: ' CPT4
           DISPLAY ' '

      * UTILISER DU TALLYING POUR COMPTER LES DELIMITEURS
           INSPECT PARM-CONTENT TALLYING DLTRCPT FOR ALL '?'
           INSPECT PARM-CONTENT TALLYING DLTRCPT FOR ALL ','
           INSPECT PARM-CONTENT TALLYING DLTRCPT FOR ALL '-'
           DISPLAY 'NOMBRE DE DELIMITEURS UTILISES'
           DISPLAY 'DLTRCPT: ' DLTRCPT
           DISPLAY ' '

      * AFFICHAGE DE CARACTERES EN MINUSCULE
           DISPLAY 'AFFICHAGE DE PARM EN MINISCULES'
           DISPLAY FUNCTION LOWER-CASE(PARM-CONTENT)
           DISPLAY ' '

      * UTILISER STRING AVEC UN POINTEUR
           STRING WS-PTR-IN DELIMITED BY SPACE
             INTO WS-STRING WITH POINTER WS-PTR
           END-STRING
           DISPLAY 'AFFICHAGE DE LA CHAINE MODIFIEE AVEC POINTEUR'
           DISPLAY WS-STRING
           DISPLAY ' '

           GOBACK.

       INITIALIZE-VAR.
      * INITIALISATION DES ZONES
           INITIALIZE CHG-CHAR.
           INITIALIZE ZONES.
           INITIALIZE CPT.
           INITIALIZE DLTR.

       RECUPERER-PARM.
      * RECUPERATION DU PARM DE LA CARTE EXEC ET UNPACKING
           UNSTRING PARM-CONTENT
             DELIMITED BY '?' OR  ',' OR  '-' OR SPACE
             INTO Z1 DELIMITER IN DLTR1 COUNT IN CPT1
                  Z2 DELIMITER IN DLTR2 COUNT IN CPT2
                  Z3 DELIMITER IN DLTR3 COUNT IN CPT3
                  Z4 COUNT IN CPT4
           END-UNSTRING
           .

       IDENTIFICATION DIVISION.
       PROGRAM-ID. ALGO71.
       AUTHOR. QUENTIN POTEREK
       DATE-WRITTEN. 7 DECEMBRE 2021

      *
       ENVIRONMENT DIVISION.

       CONFIGURATION SECTION.
       SOURCE-COMPUTER. IBM-370.
      * SOURCE-COMPUTER. IBM-370 WITH DEBUGGING MODE.
       OBJECT-COMPUTER. IBM-370.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT NOTES ASSIGN TO NOTES
             FILE STATUS WS-F-NO.

      *
       DATA DIVISION.

       FILE SECTION.
       FD NOTES RECORDING F.
       01 ENOTES.
         05 NO-CLASSE PIC XX.
         05 NO-ELEVE  PIC X(14).
         05 NO-NOTE   PIC 99.
         05 NO-COEFF  PIC 9.
         05 FILLER    PIC X(61).

       WORKING-STORAGE SECTION.
      * STATUT DES FICHIERS
       77 WS-F-NO PIC XX.

      * CONTROLE
       77 WS-ELEVE PIC X(14).
       77 WS-TOT-COEFF PIC 99 VALUE 0.
       77 WS-SUM-NOTES PIC 9(4).

      * STATISTIQUES
       77 WS-NB-ELEVES PIC 99 VALUE 0.
       77 WS-MOY-ELEVE PIC 99V99 VALUE 0.
       77 WS-MOY-CLASS PIC 9(4)V99 VALUE 0.
       77 WS-MOY-MAX   PIC 99V99 VALUE 0.
       77 WS-MOY-MIN   PIC 99V99 VALUE 20.

      * IMPRESSION
       01 WS-SORTIE-HEADER.
         05 WS-SH-ELEVE PIC X(14) VALUE 'ELEVE'.
         05 FILLER PIC X(4).
         05 WS-SH-MOYENNE PIC X(7) VALUE 'MOYENNE'.
         05 FILLER PIC X(55).
       01 WS-SORTIE-ELEVE.
         05 WS-SE-ELEVE PIC X(14).
         05 FILLER PIC X(6).
         05 WS-SE-MOYENNE PIC Z9V,99.
         05 FILLER PIC X(56).
       77 WS-MOY-FORMAT PIC Z9V,99.

      *
       PROCEDURE DIVISION.
      * OUVERTURE ET VERIFICATION DE L'INTEGRITE
           OPEN INPUT NOTES
           PERFORM TEST-STAT-NO

      * AFFICHAGE DE L'EN-TETE
           PERFORM AFFICHER-EN-TETE

      * LECTURE DU PREMIER ENREGISTREMENT
           READ NOTES
           PERFORM TEST-STAT-NO

      * ITERER SUR LE FICHIER D'ENTREE
           PERFORM UNTIL WS-F-NO = '10'
             PERFORM TEST-STAT-NO

             ADD 1 TO WS-NB-ELEVES
             MOVE NO-ELEVE TO WS-ELEVE

             MOVE 0 TO WS-MOY-ELEVE
             MOVE 0 TO WS-SUM-NOTES
             MOVE 0 TO WS-TOT-COEFF

             PERFORM UNTIL NO-ELEVE NOT = WS-ELEVE OR WS-F-NO = '10'
               COMPUTE WS-SUM-NOTES = WS-SUM-NOTES + NO-NOTE * NO-COEFF
               ADD NO-COEFF TO WS-TOT-COEFF
               READ NOTES
             END-PERFORM

             COMPUTE WS-MOY-ELEVE = WS-SUM-NOTES / WS-TOT-COEFF
             PERFORM AFFICHER-MOY-ELEVE

             IF WS-MOY-ELEVE < WS-MOY-MIN
               MOVE WS-MOY-ELEVE TO WS-MOY-MIN
             END-IF
             IF WS-MOY-ELEVE > WS-MOY-MAX
               MOVE WS-MOY-ELEVE TO WS-MOY-MAX
             END-IF

             ADD WS-MOY-ELEVE TO WS-MOY-CLASS

           END-PERFORM

           CLOSE NOTES
           PERFORM TEST-STAT-NO

           COMPUTE WS-MOY-CLASS = WS-MOY-CLASS / WS-NB-ELEVES

           PERFORM AFFICHER-MOY-CLASSE

           STOP RUN.

       AFFICHER-EN-TETE.
           DISPLAY WS-SORTIE-HEADER
           MOVE ALL '-' TO WS-SH-ELEVE
           MOVE ALL '-' TO WS-SH-MOYENNE
           DISPLAY WS-SORTIE-HEADER.

       AFFICHER-MOY-ELEVE.
           MOVE WS-ELEVE TO WS-SE-ELEVE
           MOVE WS-MOY-ELEVE TO WS-SE-MOYENNE
           DISPLAY WS-SORTIE-ELEVE.

       AFFICHER-MOY-CLASSE.
           DISPLAY ' '
           MOVE WS-MOY-MIN TO WS-MOY-FORMAT
           DISPLAY 'MOYENNE MINIMALE: ' WS-MOY-FORMAT
           MOVE WS-MOY-MAX TO WS-MOY-FORMAT
           DISPLAY 'MOYENNE MAXIMALE: ' WS-MOY-FORMAT
           MOVE WS-MOY-CLASS TO WS-MOY-FORMAT
           DISPLAY 'MOYENNE GENERALE: ' WS-MOY-FORMAT.

       TEST-STAT-NO.
           IF WS-F-NO NOT = '00'
             DISPLAY 'ERREUR FICHIER NOTES: ' WS-F-NO
             MOVE 16 TO RETURN-CODE
             STOP RUN
           END-IF.
